#!/usr/bin/env perl
use warnings;
use strict;
#
# A small tool to access all the features of your irtoy
#
#

use File::Spec;

# allow the libs to be in the bin dir
use FindBin;
use lib File::Spec->catdir($FindBin::RealBin,"lib");

use Data::Dumper;
$Data::Dumper::Indent = 1;
$Data::Dumper::Sortkeys = 1;
$Data::Dumper::Quotekeys = 0;

use Getopt::Long 2.33 qw(:config gnu_getopt no_auto_abbrev);

use IRToy ':consts';

my $option = {
};
my @option_list = (
    "port=s",
    "debug",
    "print=s",
);

sub do_options {
    my $option = shift || die "no option!";
    my @option_list = grep {$_} @_;
    GetOptions($option,@option_list,'man','help|?') or pod2usage(2);
    if ($option->{help} && @option_list) {
        print("List of options:\n");
        print("\t");
        foreach(sort @option_list) {
            $_="--".$_;
        }
        print join(", ",@option_list);
        print("\n");
    }
    pod2usage(-exitstatus => 0, -verbose => 2) if $option->{man};

    if ($option->{quiet}) {
        delete $option->{verbose};
    }
}

# Quick and dirty conversion from byte to a line-drawing binary
sub byte2binstr {
    my $byte = shift;
    my $prev = shift;

    my $digit_0 = chr(0x2502).' ';
    my $digit_1 = ' '.chr(0x2503);
    my $digit_01 = chr(0x2515).chr(0x2513);
    my $digit_10 = chr(0x250D).chr(0x251B);
    my $spacer = '  ';

    my $s=$spacer;
    for (1..8) {
        if      ( ($prev & 0x1) &&  ($byte & 0x1)) {
            # both prev and this are 1's
            $s.=$digit_1;
        } elsif (!($prev & 0x1) && !($byte & 0x1)) {
            # both prev and this are 0's
            $s.=$digit_0;
        } elsif (!($prev & 0x1) &&  ($byte & 0x1)) {
            # transition from 0 to 1
            $s.=$digit_01;
        } elsif ( ($prev & 0x1) && !($byte & 0x1)) {
            # transition from 1 to 0
            $s.=$digit_10;
        } else {
            # cannot happen!
            die;
        }
        $s.=$spacer;
        $byte = $byte >>1;
        $prev = $prev >>1;
    }
    return $s;
}

sub print_tracebuf {
    my $buf = shift;

    binmode(STDOUT, ":utf8");
    print("  0   1   2   3   4   5   6   7\n");
    my $prevbyte;
    for my $byte (split(//,$buf)) {
        print(byte2binstr(ord($byte),ord($prevbyte)),"\n");
        $prevbyte=$byte;
    }
}

sub print_buf {
    my $buf = shift;
    if ($option->{print} && $option->{print} eq 'trace') {
        print_tracebuf($buf);
    }
}

sub main() {
    do_options($option,@option_list);
    return if (defined($option->{help}));

    die("Please specify port") if (!defined($option->{port}));

    my $toy = IRToy->new()->open($option->{port}) || die("couldnt open");
    $toy->debug($option->{debug});

    # Confirm that we have working comms with the device
    die("Can not communicate") if (!defined($toy->checkmode(IRToy::MODE_RC)));

    my $cmd = $ARGV[0] || '';
    if ($cmd eq 'check') {
        # done already above, so.. do nothing here
    #} elsif ($cmd eq 'rc_read') {
    #    while(1) {
    #        $toy->rc_read();
    #    }
    } elsif ($cmd eq 'led') {
        $toy->led($ARGV[1]);
    } elsif ($cmd eq 'led_mute') {
        $toy->led($ARGV[1]);
    } elsif ($cmd eq 'selftest') {
        # TODO - pass or fail result
        print($toy->selftest(),"\n");
    } elsif ($cmd eq 'sample_settings') {
        # TODO - this simply prints binary data!
        print($toy->sample_settings(),"\n");
    } elsif ($cmd eq 'sumpmeta') {
        # TODO - this simply prints binary data!
        print($toy->sump_meta(),"\n");
    } elsif ($cmd eq 'sumprun') {
        my $buf = $toy->sump_run();
        print_buf($buf);
    } elsif ($cmd eq 'version') {
        print($toy->get_version(),"\n");
    } elsif ($cmd eq 'raw') {
        $toy->write(pack('H*',$ARGV[1]));
        my $maxlen = $ARGV[2];

        my $data = '';
        while (length($data) < $maxlen) {
            my ($count,$buf) = $toy->read(128);
            $data .= $buf;
        }
    } else {
        die("unknown command");
    }

}
main();

